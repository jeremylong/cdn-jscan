import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'vuln.tools.java-application-conventions'
    id 'org.springframework.boot' version '2.7.5'
    id 'io.spring.dependency-management' version '1.0.15.RELEASE'
}

description = 'A java CLI scan for references to hosted JS in application files'

dependencies {
    implementation project(':cdnjs-api')
    implementation 'info.picocli:picocli-spring-boot-starter:4.6.3'
    implementation 'com.diogonunes:JColor:5.5.1'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.4.2'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.13.4'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}
jar {
    enabled = false
}
bootJar {
    launchScript()
}

application {
    mainClass = 'io.github.jeremylong.vulnz.cli.Application'
}

task generateAutocomplete(type: JavaExec) {
    mainClass = 'picocli.AutoComplete'
    classpath = sourceSets.main.runtimeClasspath
    args = ['--force', '--name', 'vulnz', '--completionScript', "${buildDir}/resources/main/vulnz.completion.sh", 'io.github.jeremylong.vulnz.cli.commands.MainCommand']
}
tasks.named("classes") { finalizedBy("generateAutocomplete") }

processResources {
    filter ReplaceTokens, tokens: [version: project.version]
}

publishing {
    publications {
        maven(MavenPublication) {
            artifact tasks.named("bootJar")
            pom {
                url = 'https://github.com/jeremylong/cdn-jscan/'
                description = project.description
                name = project.name
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://github.com/jeremylong/cdn-jscan/blob/main/LICENSE.txt'
                    }
                }
                developers {
                    developer {
                        id = 'jeremy.long'
                        name = 'Jeremy Long'
                    }
                }
                scm {
                    url = 'https://github.com/jeremylong/cdn-jscan'
                    connection = 'scm:git:https://github.com/jeremylong/cdn-jscan.git'
                    developerConnection = 'scm:git:https://github.com/jeremylong/cdn-jscan.git'
                }
            }
        }
    }
}
